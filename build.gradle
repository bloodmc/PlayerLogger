buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.1'
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '2.0.1'
    id 'maven'
}

// Environment variables for the build set by the build server
ext.buildNumber = System.env.BUILD_NUMBER ?: '0'

defaultTasks 'clean', 'build'

allprojects {
    apply plugin: 'java'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

archivesBaseName = project.name.toLowerCase()

version = "0.1"

project.ext.getGitHash = {
    def command = Runtime.getRuntime().exec("git rev-parse --short HEAD")
    def result = command.waitFor()
    return (result == 0) ? command.inputStream.text.trim() : "nogit"
}

repositories {
    mavenCentral()
    maven {
        url = 'https://maven.oracle.com'
    }
    maven {
        name = 'sponge'
        url = 'https://repo.spongepowered.org/maven'
    }
    maven {
        name = 'spigot'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
}

dependencies {
    compile 'com.typesafe:config:1.3.1'
    compile 'org.spongepowered:configurate-core:3.7-SNAPSHOT'
    compile 'org.spongepowered:configurate-hocon:3.7-SNAPSHOT'
    compileOnly 'commons-io:commons-io:2.6'
    compileOnly 'org.bukkit:bukkit:1.13.1-R0.1-SNAPSHOT'
}

jar {
    manifest.attributes('Implementation-Title': 'PlayerLogger')
    manifest.attributes('Implementation-Version': "$version")
    manifest.attributes('Git-Hash': project.ext.getGitHash())
    manifest.attributes('Main-Class': 'me.bloodmc.playerlogger.PlayerLoggerPlugin')
    classifier = ''
}

artifacts {
    archives shadowJar
}

shadowJar {
    classifier = ''

    dependencies {
        include dependency("com.typesafe:config:1.3.1")
        include dependency("org.spongepowered:configurate-core:3.7-SNAPSHOT")
        include dependency("org.spongepowered:configurate-hocon:3.7-SNAPSHOT")
    }

    exclude "dummyThing"
}